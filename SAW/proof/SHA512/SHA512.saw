/* 
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
*/
// Include SHA512 helper functions
include "SHA512-common.saw";

// Include rewrite rules
include "goal-rewrites.saw";

// Verify the `EVP_SHA_INIT` C function satisfies the `EVP_sha_init_spec`
// specification
llvm_verify m EVP_SHA_INIT [] true EVP_sha_init_spec (w4_unint_yices []);


// Verify the `EVP_DigestInit` C function satisfies the
// `EVP_DigestInit_array_spec` unbounded specification.
llvm_verify m "EVP_DigestInit"
  [ OPENSSL_malloc_init_ov
  , OPENSSL_free_null_ov
  ]
  true
  EVP_DigestInit_array_spec
  (do {
    goal_eval_unint [];
    w4_unint_z3 [];
  });

// Verify the `EVP_DigestUpdate` C function satisfies the
// `EVP_DigestUpdate_array_spec` unbounded specification.
EVP_DigestUpdate_array_ov <- llvm_verify m "EVP_DigestUpdate"
  [sha512_block_data_order_array_ov]
  true
  EVP_DigestUpdate_array_spec
  (do {
    goal_eval_unint ["processBlocks", "processBlock_Common"];
    simplify (addsimps [processBlocks_0_1_thm] empty_ss);
    simplify (addsimps [arrayRangeEq_arrayRangeLookup_eq_thm, arrayCopy_zero_thm] empty_ss);
    simplify (addsimps append_ite_thms empty_ss);
    goal_eval_unint ["processBlocks", "processBlock_Common"];
    w4_unint_z3 ["processBlocks", "processBlock_Common"];
  });

// Verify the `EVP_DigestFinal` C function satisfies the
// `EVP_DigestFinal_array_spec` unbounded specification.
// TODO:
// Currently this proof fails due to an undefined behaviour.
// Specifically, when results in sha->h[i] are copied into out,
// LLVM does an optimization using vectorized bswap. This vectorized
// bswap requires that memory region of sha->h and out are non-overlapping.
// To ensure the non-overlapping condition, in LLVM IR, it does two comparisons:
// overlapping = end(sha->h) > begin(out) && end(out) > begin(sha->h)
// This comparison compares pointers from different locations,
// triggers an undefined behaviour and therefore SAW errors.
let verify_final_with_length withLength = do {
  print (str_concat "Verifying EVP_DigestFinal withLength=" (show withLength));
  enable_what4_eval;
  llvm_verify m "EVP_DigestFinal"
    [ sha512_block_data_order_array_ov
    , OPENSSL_free_nonnull_ov
    , OPENSSL_cleanse_ov
    ]
    true
    (EVP_DigestFinal_array_spec withLength)
    (do {
      goal_eval_unint ["processBlock_Common"];
      simplify (addsimps [arrayUpdate_arrayCopy_thm, arraySet_zero_thm] empty_ss);
      simplify (addsimps [bvult_64_32_thm] empty_ss);
      simplify (addsimps append_ite_thms empty_ss);
      goal_eval_unint ["processBlock_Common"];
      w4_unint_z3 ["processBlock_Common"];
    });
  disable_what4_eval;
};
for [false, true] verify_final_with_length;

